## This file is part of GNU Dico
## Copyright (C) 2014-2019 Sergey Poznyakoff
##
## GNU Dico is free software; you can redistribute it and/or modify
## it under the terms of the GNU General Public License as published by
## the Free Software Foundation; either version 3, or (at your option)
## any later version.
##
## GNU Dico is distributed in the hope that it will be useful,
## but WITHOUT ANY WARRANTY; without even the implied warranty of
## MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
## GNU General Public License for more details.
##
## You should have received a copy of the GNU General Public License
## along with GNU Dico.  If not, see <http://www.gnu.org/licenses/>.

## module description: WordNet

AC_SUBST(WORDNET_LOPT)
AC_SUBST(WORDNET_IOPT)
AC_ARG_WITH([wordnet],
            AC_HELP_STRING([--with-wordnet@<:@=DIR@:>@],
                           [use wordnet]),
            [
case "${withval}" in
  yes) status_wordnet=req ;;
  no)  status_wordnet=no ;;
  /*)  status_wordnet=req
       WORDNET_LOPT=-L${withval}/lib
       WORDNET_IOPT=-I${withval}/include;;
  *)   AC_MSG_ERROR([argument for --with-wordnet must be an absoulute directory name]) ;;
esac],[status_wordnet=maybe])

AC_SUBST([WORDNET_LIBWN],[WN])
AC_ARG_WITH([libWN],
            AC_HELP_STRING([--with-libWN@<:@=NAME@:>@],
                           [use libNAME instead of libWN]),
            [WORDNET_LIBWN=${withval#lib}])

if test $status_wordnet != no; then
  save_LIBS=$LIBS
  save_CPPFLAGS=$CPPFLAGS
  LIBS="$LIBS $WORDNET_LOPT"
  CPPFLAGS="$CPPFLAGS $WORDNET_IOPT"
  AC_CHECK_HEADER([wn.h],
      [AC_CHECK_LIB($WORDNET_LIBWN, findtheinfo, status_wordnet=yes)])
  case $status_wordnet in
  req) AC_MSG_ERROR([required module wordnet cannot be built because of missing prerequisites]);;
  maybe) status_wordnet=no;;
  esac
  LIBS=$save_LIBS
  CPPFLAGS=$save_CPPFLAGS
fi

AM_CONDITIONAL([WORDNET_COND],[test $status_wordnet = yes])
