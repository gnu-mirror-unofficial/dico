## This file is part of GNU Dico
## Copyright (C) 2014-2019 Sergey Poznyakoff
##
## GNU Dico is free software; you can redistribute it and/or modify
## it under the terms of the GNU General Public License as published by
## the Free Software Foundation; either version 3, or (at your option)
## any later version.
##
## GNU Dico is distributed in the hope that it will be useful,
## but WITHOUT ANY WARRANTY; without even the implied warranty of
## MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
## GNU General Public License for more details.
##
## You should have received a copy of the GNU General Public License
## along with GNU Dico.  If not, see <http://www.gnu.org/licenses/>.

AC_ARG_WITH([python],
            AC_HELP_STRING([--without-python],
                           [do not build Python interface]),
            [
case "${withval}" in
  yes) status_python=yes ;;
  no)  status_python=no ;;
  *)   AC_MSG_ERROR(bad value ${withval} for --without-python) ;;
esac],[status_python=yes])

PYTHON3_COND=0
if test "$status_python" = yes; then
  AC_ARG_VAR([PYTHON], [The name of python binary])
  AC_PATH_PROG([PYTHON], python)
  
  AC_ARG_VAR([PYTHON_CONFIG], [The name of python-config binary])
  AC_PATH_PROG([PYTHON_CONFIG], python-config)
  if test -n "$PYTHON_CONFIG"; then
    AC_SUBST(PYTHON_LIBS,`$PYTHON_CONFIG --libs`)
    AC_SUBST(PYTHON_INCLUDES,`$PYTHON_CONFIG --includes`)
    $PYTHON <<EOT
import sys
exit(1 if sys.version_info[[0]] == 3 else 0)
EOT
    PYTHON3_COND=$?
  else
    status_python=no     
  fi	
fi
AM_CONDITIONAL([PYTHON_COND],[test $status_python = yes])
AM_CONDITIONAL([PYTHON3_COND],[test $PYTHON3_COND -eq 1])
