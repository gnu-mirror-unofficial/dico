## This file is part of GNU Dico
## Copyright (C) 2014-2021 Sergey Poznyakoff
##
## GNU Dico is free software; you can redistribute it and/or modify
## it under the terms of the GNU General Public License as published by
## the Free Software Foundation; either version 3, or (at your option)
## any later version.
##
## GNU Dico is distributed in the hope that it will be useful,
## but WITHOUT ANY WARRANTY; without even the implied warranty of
## MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
## GNU General Public License for more details.
##
## You should have received a copy of the GNU General Public License
## along with GNU Dico.  If not, see <http://www.gnu.org/licenses/>.

AC_ARG_WITH([python],
            AC_HELP_STRING([--without-python],
                           [do not build Python interface]),
              [
case "${withval}" in
  yes) status_python=yes ;;
  no)  status_python=no ;;
  *)   AC_MSG_ERROR(bad value ${withval} for --with-python) ;;
esac],[status_python=yes])

AC_SUBST(PYTHON_LIBS)
AC_SUBST(PYTHON_INCLUDES)

if test "$status_python" = yes; then
  AM_PATH_PYTHON(3.0,, [status_python=no])
  if test "$status_python" = yes; then
    AC_ARG_VAR([PYTHON_CONFIG], [The name of python-config binary])
    if test -z "$PYTHON_CONFIG"; then
      AC_PATH_PROGS([PYTHON_CONFIG], [python3-config python-config])
    fi
    if test -n "$PYTHON_CONFIG"; then
      save_CPPFLAGS=$CPPFLAGS
      CPPFLAGS=$($PYTHON_CONFIG --includes)
      AC_MSG_CHECKING([whether python-config ($PYTHON_CONFIG) is of same version as python])
      AC_EGREP_CPP([^\"$PYTHON_VERSION],
        [#include <patchlevel.h>
PY_VERSION],
	[AC_MSG_RESULT([yes])],
	[AC_MSG_RESULT([no])
	 status_python=no])
      CPPFLAGS=$save_CPPFLAGS
    else
      status_python=no
    fi
   
    if test "$status_python" = yes; then
      # See https://docs.python.org/dev/whatsnew/3.8.html#debug-build-uses-the-same-abi-as-release-build
      PYTHON_LIBS=`$PYTHON_CONFIG --libs --embed 2>/dev/null` || \
         PYTHON_LIBS=`$PYTHON_CONFIG --libs`
      PYTHON_INCLUDES=`$PYTHON_CONFIG --includes`
      
      AC_DEFINE(WITH_PYTHON,1,[Enable Python support])
      MU_LIB_PY='${top_builddir}/python/libmu_py/libmu_py.la'
      AC_SUBST([PYTHON_SITE_DIR])
      AC_SUBST([PYTHON_EXEC_DIR])
      AC_ARG_WITH([pythondir],
           AC_HELP_STRING([--with-pythondir=DIR],
                          [Specify the directory to install Python modules to]),
           [case $withval in
            /*)  PYTHON_SITE_DIR="${withval}/mailutils"
	         PYTHON_EXEC_DIR="${withval}/mailutils";;
            *)   AC_MSG_ERROR([Argument to --with-pythondir must be an absolute directory name]);;
            esac],
	   [PYTHON_SITE_DIR='$(pkgpythondir)'
	    PYTHON_EXEC_DIR='$(pkgpyexecdir)'])
    fi
  fi
fi

AM_CONDITIONAL([PYTHON_COND],[test $status_python = yes])
